dependencies {
    testImplementation "com.h2database:h2"
}

def profiles = "prod"
if (project.hasProperty("no-liquibase")) {
    profiles += ",no-liquibase"
}

if (project.hasProperty("api-docs")) {
    profiles += ",api-docs"
}

springBoot {
    buildInfo()
}

bootRun {
    doFirst {
        if (!project.hasProperty("includeWebapp")) {
            throw new GradleException("Required profile 'includeWebapp' missing. Set it with '-PincludeWebapp'.")
        }
    }
    args = []
}

bootJar {
    doFirst {
        if (!project.hasProperty("includeWebapp")) {
            throw new GradleException("Required profile 'includeWebapp' missing. Set it with '-PincludeWebapp'.")
        }
    }
}

task webapp(type: NpmTask, dependsOn: "npm_install") {
    args = ["run", "webapp:prod"]
    environment = [APP_VERSION: project.version]
}

processResources {
    inputs.property('version', version)
    inputs.property('springProfiles', profiles)
    filesMatching("**/application.yml") {
        filter {
            it.replace("#project.version#", version)
        }
        filter {
            it.replace("#spring.profiles.active#", profiles)
        }
    }
}

// Autocut: speed up build
if (project.hasProperty("includeWebapp")) {
    processResources.dependsOn webapp
    bootJar.dependsOn processResources
}
